# This sets up MBED all MBED libraries, then sets up the robot and base station
# firmware, which depend on the MBED libs

# Set the mbed target processor and settings for cmake
set(MBED_TARGET LPC1768)
set(MBED_USE_RTOS true)
set(MBED_USE_NET false)
set(MBED_USE_USB false)
set(MBED_USE_DSP false)

# The name of the toolchain we're using.  Note: we don't actually set the CMake
# toolchain here because it would require that all files compiled under this
# CMakeLists file to be compiled for the MBED, which we don't want.  We want out
# tests and other things to be compiled for the host computer.
set(MBED_TOOLCHAIN TOOLCHAIN_GCC_ARM)

# sets many variables and paths related to building for the MBED
include(mbed/arm_mbed.cmake)
# Note: the arm_mbed.cmake script exports flags that need to be set in each CMake
# file used to build MBED code like so:
# set(CMAKE_CXX_FLAGS ${MBED_CMAKE_CXX_FLAGS})
# set(CMAKE_C_FLAGS ${MBED_CMAKE_C_FLAGS})
# set(CMAKE_EXE_LINKER_FLAGS ${MBED_CMAKE_EXE_LINKER_FLAGS})

# enable C++ exceptions
set(MBED_CMAKE_CXX_FLAGS "${MBED_CMAKE_CXX_FLAGS} -fexceptions")

# enable C++11
set(MBED_CMAKE_CXX_FLAGS "${MBED_CMAKE_CXX_FLAGS} -std=c++11")

# suppress -rdynamic flag
set(CMAKE_SHARED_LIBRARY_LINK_C_FLAGS "")
set(CMAKE_SHARED_LIBRARY_LINK_CXX_FLAGS "")

# Program that generates a lookup table (large array of floats) for LQR gain
# matrices based on the Robot2015ModelParams in RobotModel.cpp.  Note: this is
# put here rather than in the cpu/motion_control folder because any CMake
# commands in or under the cpu directory are compiled for the MBED, not the host
# computer.
include_directories(../firmware/robot2015/cpu/motion_control)
set(LOOKUP_TABLE_GEN_SRC
    "robot2015/cpu/motion_control/LqrLookupTableGenerator.cpp"
    "robot2015/cpu/motion_control/RobotModel.cpp"
    "robot2015/cpu/motion_control/TunableRobotParameters.cpp"
)
set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -std=c++11 -fexceptions")
add_executable(lqr-lookup-table-gen ${LOOKUP_TABLE_GEN_SRC})
# only build lookup table generator if needed
set_target_properties(lqr-lookup-table-gen PROPERTIES EXCLUDE_FROM_ALL TRUE)
# put the binary under the /build directory instead of /run
set_target_properties(lqr-lookup-table-gen PROPERTIES RUNTIME_OUTPUT_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR})

# header files
include_directories("common2015/drivers" )
include_directories("common2015/drivers/CC1101")
include_directories("common2015/modules" )
include_directories("common2015/modules/CommLink")
include_directories("common2015/modules/CommModule")
include_directories("common2015/utils")
include_directories("common2015/utils/FirmwareCheck")
include_directories("common2015/utils/Logger")
include_directories("common2015/utils/RTOS_Management")
include_directories("common2015/utils/Watchdog")

# Add a test runner target "test-firmware"
set(FIRMWARE_TEST_SRC
    "common2015/ExampleTest.cpp"
)
add_executable(test-firmware ${FIRMWARE_TEST_SRC})
target_link_libraries(test-firmware ${GTEST_BOTH_LIBRARIES})
add_dependencies(test-firmware googletest)
# Don't build the tests by default
set_target_properties(test-firmware PROPERTIES EXCLUDE_FROM_ALL TRUE)


# build robot and base station firmware and the library that they depend on
add_subdirectory(mbed)
add_subdirectory(common2015)
add_subdirectory(base2015)
add_subdirectory(robot2015)
