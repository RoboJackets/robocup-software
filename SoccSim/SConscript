Import('*')

e = env.Clone()

physx_path=Dir('#/SoccSim/lib/physx')
physx_include=physx_path.Dir('include')
physx_libpath = physx_path.Dir('lib')

e.Append(CPPPATH=[
	physx_include.Dir('Physics/include'),
	physx_include.Dir('Foundation/include'),
	physx_include.Dir('PhysXLoader/include'),
	physx_include.Dir('Cooking/include')])

# Link with PhysX libraries
e.Append(LIBPATH=physx_libpath)
e.Append(LIBS=['NxCooking', 'PhysXLoader'])

# Flags required for PhysX on Linux
e.MergeFlags('-DNX32 -DLINUX -DNX_DISABLE_FLUIDS')

p = e.Program('soccsim', [
	'main.cpp',
	'Viewer.cpp',
	'Config.cpp',
	'Physics/Env.cpp',
	'Physics/Entity.cpp',
	'Physics/Ball.cpp',
	'Physics/Field.cpp',
	'Physics/Robot.cpp',
	'Physics/MemoryStream.cpp'])

e.Install(exec_dir, p)

# Copy PhysX libraries to the run directory
e.Install(exec_dir, physx_libpath.File('libNxCooking.so.1'))
e.Install(exec_dir, physx_libpath.File('libPhysXLoader.so.1'))
e.Install(exec_dir, physx_libpath.File('libPhysXCore.so.1'))

